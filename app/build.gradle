apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'net.ltgt.errorprone'
apply plugin: 'com.jakewharton.hugo'
// discover dependency current versions ./gradlew dependencyUpdates -Drevision=release
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.gladed.androidgitversion'

apply from: rootProject.file("gradle/compile.gradle")
apply from: rootProject.file("gradle/android.gradle")

android {
    defaultConfig {
        applicationId "sk.vander.electride"
        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()
        buildConfigField("String", "MAPBOX_TOKEN", "\"${rootProject.ext.props.mapbox}\"")
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        ext.betaDistributionGroupAliases = "electride"
        ext.betaDistributionReleaseNotesFilePath = "changelog.md"
    }

    signingConfigs {
        if (isTravis()) {
            travis {
                storeFile rootProject.file("private_keystore.jks")
                storePassword System.getenv("storepass")
                keyAlias System.getenv("keyalias")
                keyPassword System.getenv("keypass")
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isTravis()) {
                signingConfig = signingConfigs.travis
            }
        }
    }
}

kapt {
    correctErrorTypes = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation deps.appcompat_v7
    implementation deps.design
    implementation deps.constraint_layout
    implementation deps.lifecycle_ext
    kapt deps.lifecycle_compiler
    implementation deps.room
    implementation deps.room_rx
    kapt deps.room_compiler
    implementation deps.rxbindings_kt
    implementation deps.rxbindings_support_kt
    implementation deps.rxbindings_appcompat_kt
    implementation deps.rxbindings_design_kt
    implementation deps.rxbindings_recyclerview_kt
    implementation(deps.mapbox) { transitive = true }
    implementation deps.mapbox_services_rx
    kapt deps.butterknife_compiler
    implementation deps.picasso
    implementation deps.rounded_image
    implementation deps.fab_menu

    implementation deps.kotlin
    implementation deps.paperparcel
    implementation deps.paperparcel_kt
    kapt deps.paperparcel_compiler
    kapt deps.dagger_compiler
    kapt deps.dagger_android_compiler
    kapt deps.autodagger_compiler
    compileOnly deps.javax_annotations
    implementation deps.rx_location
    implementation deps.rx_permissions
    implementation deps.three_ten
    implementation deps.rx_pref

    implementation deps.retrofit
    implementation deps.retrofit_rxjava
    implementation deps.retrofit_gson
    implementation deps.okhttp
    implementation deps.okhttp_logging

    implementation(deps.crashlytics) { transitive = true }

    debugImplementation deps.leakcanary
    debugImplementation deps.blockcanary
    debugImplementation deps.stetho
    debugImplementation deps.stetho_okhttp
    debugImplementation deps.stetho_js

    testImplementation deps.junit

    androidTestImplementation(deps.espresso, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation project(':lib')
}